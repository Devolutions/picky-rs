#!/usr/bin/env -S just --justfile

# This is just intended to help developer, no need in CI #

############ `just` is required ##############
# ------------------------------------------ #
# https://github.com/casey/just#installation #
# ------------------------------------------ #
# Easy way is: cargo install just            #
##############################################

default: bindings

## Diplomat installation ##

diplomat_rev := "6f569bd27bc1c2e919fe09f36a5053009593a7bd"

install-diplomat:
    cargo install --git https://github.com/CBenoit/diplomat.git --rev {{diplomat_rev}} diplomat-tool -f

## Native library ##

target_debug_folder   := "../target/debug/"
target_release_folder := "../target/release/"
shared_lib_name_linux := "libpicky.so"
shared_lib_name_win   := "picky.dll"

dotnet_diplomat_config := "./dotnet-interop-conf.toml"
dotnet_path            := "./dotnet/"
dotnet_lib_path        := dotnet_path + "Devolutions.Picky/"
dotnet_generated_path  := dotnet_lib_path + "Generated/"

runtimes_path  := "../dependencies/runtimes/"
lib_path_linux := runtimes_path + "linux-x64/native/"
lib_path_win   := runtimes_path + "win-x64/native/"

native-lib-debug:
    cargo build -p picky-ffi
    mkdir -p {{lib_path_linux}}
    -cp {{target_debug_folder}}{{shared_lib_name_linux}} {{lib_path_linux}}
    mkdir -p {{lib_path_win}}
    -cp {{target_debug_folder}}{{shared_lib_name_win}} {{lib_path_win}}

native-lib-release:
    cargo build -p picky-ffi --release
    mkdir -p {{lib_path_linux}}
    -cp {{target_release_folder}}{{shared_lib_name_linux}} {{lib_path_linux}}
    mkdir -p {{lib_path_win}}
    -cp {{target_release_folder}}{{shared_lib_name_win}} {{lib_path_win}}

## Bindings ##

bindings: dotnet-bindings

dotnet-bindings:
    -rm {{dotnet_generated_path}}*.cs
    diplomat-tool dotnet {{dotnet_generated_path}} -l {{dotnet_diplomat_config}}
    @echo ">> .NET wrapper generated at {{dotnet_generated_path}}"

## Tests ##

test: test-dotnet

test-dotnet: native-lib-debug dotnet-bindings
    cd {{dotnet_path}} && dotnet test
